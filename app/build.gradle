plugins {
    id 'com.android.application'
}

apply plugin: 'com.google.gms.google-services'
apply plugin: 'com.google.firebase.firebase-perf'
apply plugin: 'com.google.firebase.crashlytics'

android {
    compileSdkVersion 31
    buildToolsVersion "30.0.3"

    defaultConfig {
        applicationId "com.chiragjn.template"
        minSdkVersion 23
        targetSdkVersion 31

        def versionMajor = 1
        def versionMinor = 0
        def versionPatch = 0

        versionName buildVersionName(versionMajor, versionMinor, versionPatch)
        versionCode buildVersionCode(versionMajor, versionMinor, versionPatch)

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        debug {
            zipAlignEnabled false
            debuggable true
            minifyEnabled false
            shrinkResources false
        }
        release {
            zipAlignEnabled true
            debuggable false
            minifyEnabled true
            shrinkResources true
            ext.enableCrashlytics = true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    buildFeatures {
        viewBinding true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    def googleServicesFile = file("google-services.json")
    if (!googleServicesFile.exists()) {
        project.logger.lifecycle('Please create app/google-services.json to communicate with Firebase.')
    }
}

static def buildVersionName(versionMajor, versionMinor, versionPatch) {
    return "${versionMajor}.${versionMinor}.${versionPatch}"
}

static def buildVersionCode(versionMajor, versionMinor, versionPatch) {
    return versionMajor * 10**2 + versionMinor * 10**1 + versionPatch * 10**0
}

dependencies {

    implementation 'androidx.appcompat:appcompat:1.3.1'
    implementation 'com.google.android.material:material:1.4.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.0'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'

    //    Firebase
    implementation 'com.google.firebase:firebase-crashlytics:18.2.1'
    implementation 'com.google.firebase:firebase-analytics:19.0.1'
    implementation 'com.google.firebase:firebase-auth:21.0.1'
    implementation 'com.google.firebase:firebase-perf:20.0.2'
}